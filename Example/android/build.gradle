buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 35
        ndkVersion = "27.1.12297006"
        kotlinVersion = "2.0.21"
    }
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal() // Added to run node ./scripts/codegen-sync-archs.js using syncArchs task
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("com.github.node-gradle:gradle-node-plugin:7.0.2") // Added to run node ./scripts/codegen-sync-archs.js using syncArchs task
    }
}

// Detox repository for `com.wix:detox` dependency specified
// in app build.gradle. This most likely does not have to be
// specified for each project but AFAIK it does not create any conflicts.
// See: https://github.com/wix/Detox/issues/3973#issuecomment-1459895555
// TODO: Find out which project should have it added.
allprojects {
    repositories {
        maven {
            url("$rootDir/../node_modules/detox/Detox-android")
        }
    }
}

apply plugin: "com.facebook.react.rootproject"

// Custom configuration. This is done in order to simplify process
// of upgrading the wrapper, eliminating necessity to manually specify CLI options
// to ensure that we use distribution of type `ALL`.
tasks.named("wrapper") {
    it.distributionType = Wrapper.DistributionType.ALL
}
